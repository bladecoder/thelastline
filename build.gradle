buildscript {
  repositories {
    mavenCentral()
    gradlePluginPortal()
    google()
  }
  dependencies {
    classpath "com.android.tools.build:gradle:8.7.0"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:6.25.0"

  }
}

allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  // This allows you to "Build and run using IntelliJ IDEA", an option in IDEA's Settings.
  idea {
    module {
      outputDir = file('build/classes/java/main')
      testOutputDir = file('build/classes/java/test')
    }
  }
}

configure(subprojects - project(':android')) {
  apply plugin: 'java-library'
  apply plugin: 'com.diffplug.spotless'
  java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }
  compileJava {
    options.incremental = true
  }
  spotless {
    java {
      target 'src/*/java/**/*.java'
      googleJavaFormat('1.17.0')
      removeUnusedImports()
      trimTrailingWhitespace()
      endWithNewline()
    }
  }
  // From https://lyze.dev/2021/04/29/libGDX-Internal-Assets-List/
  // The article can be helpful when using assets.txt in your project.
  compileJava.doLast {
    // projectFolder/assets
    def assetsFolder = new File("${project.rootDir}/assets/")
    // projectFolder/assets/assets.txt
    def assetsFile = new File(assetsFolder, "assets.txt")
    // delete that file in case we've already created it
    assetsFile.delete()

    // iterate through all files inside that folder
    // convert it to a relative path
    // and append it to the file assets.txt
    fileTree(assetsFolder).collect { assetsFolder.relativePath(it) }.each {
      assetsFile.append(it + "\n")
    }
  }
}

subprojects {
  version = '1.0.0'
  ext.appName = 'thelastline'
  repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
  }
}

eclipse.project.name = 'thelastline' + '-parent'
